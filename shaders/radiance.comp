#version 460

#include "extensions.glsl"

#define PI 3.141593f
#define EPSILON 0.000001f

#include "utils.glsl"

layout(binding = 0) uniform samplerCube skyboxTex;

layout(r32ui, binding = 1) uniform restrict writeonly uimageCube outputTex;

vec2 hammersley(u32 i, u32 N) {
    u32 bits = i;
    bits = (bits << 16u) | (bits >> 16u);
    bits = ((bits & 0x55555555u) << 1u) | ((bits & 0xAAAAAAAAu) >> 1u);
    bits = ((bits & 0x33333333u) << 2u) | ((bits & 0xCCCCCCCCu) >> 2u);
    bits = ((bits & 0x0F0F0F0Fu) << 4u) | ((bits & 0xF0F0F0F0u) >> 4u);
    bits = ((bits & 0x00FF00FFu) << 8u) | ((bits & 0xFF00FF00u) >> 8u);
    return vec2(f32(i) / f32(N), f32(bits) * 2.3283064365386963e-10);
}

f32 distributionGGX(vec3 normal, vec3 halfway, f32 roughness) {
    f32 alpha = roughness * roughness;
    f32 alphaSquared = alpha * alpha;
    f32 nDotH = max(dot(normal, halfway), 0.0f);
    f32 nDotHSquared = nDotH * nDotH;
    f32 bottomTerm = nDotHSquared * (alphaSquared - 1.0f) + 1.0f;
    return alphaSquared / (PI * bottomTerm * bottomTerm);
}

vec3 importanceSampleGGX(vec2 Xi, vec3 normal, f32 roughness) {
    f32 a = roughness * roughness;
	
    f32 phi = 2.0 * PI * Xi.x;
    f32 cosTheta = sqrt((1.0 - Xi.y) / (1.0 + (a*a - 1.0) * Xi.y));
    f32 sinTheta = sqrt(1.0 - cosTheta*cosTheta);
    vec3 H = vec3(cos(phi) * sinTheta, sin(phi) * sinTheta, cosTheta);

    vec3 up = abs(normal.z) < 0.999 ? vec3(0.0, 0.0, 1.0) : vec3(1.0, 0.0, 0.0);
    vec3 tangent = normalize(cross(up, normal));
    vec3 bitangent = cross(normal, tangent);
	
    vec3 sampleVec = tangent * H.x + bitangent * H.y + normal * H.z;
    return normalize(sampleVec);
} 

layout(local_size_x = 8, local_size_y = 8) in;
void main() {
    f32 resolution = textureSize(skyboxTex, 0).x;
    f32 roughness = (countMips(textureSize(skyboxTex, 0)) - countMips(imageSize(outputTex))) / (countMips(textureSize(skyboxTex, 0)) - 1.0f);
    
    vec3 pos = 2.0f * vec3(gl_GlobalInvocationID.xy / vec2(imageSize(outputTex)), 1.0f) - 1.0f;
	vec3 faces[6] = {
		vec3( pos.z, -pos.y, -pos.x),
		vec3(-pos.z, -pos.y,  pos.x),
		vec3( pos.x,  pos.z,  pos.y),
		vec3( pos.x, -pos.z, -pos.y),
		vec3( pos.x, -pos.y,  pos.z),
		vec3(-pos.x, -pos.y, -pos.z)
	};
	vec3 normal = normalize(faces[gl_GlobalInvocationID.z]);

    const u32 sampleCount = 1024u;
    f32 totalWeight = 0.0f;
    vec3 prefilterColor = vec3(0.0f);

    for(u32 i = 0u; i < sampleCount; i++) {
        vec2 Xi = hammersley(i, sampleCount);
        vec3 H = importanceSampleGGX(Xi, normal, roughness);
        vec3 L = normalize(2.0f * dot(normal, H) * H - normal);
        f32 nDotL = max(dot(normal, L), 0.0f);
        if(nDotL > 0.0f) {
            f32 pdf = distributionGGX(normal, H, roughness) * max(dot(normal, H), 0.0f) / (4.0f * max(dot(H, normal), 0.0f)) + EPSILON;
            f32 mip = roughness == 0.0f ? 0.0f : 0.5f * log2((1.0f / (sampleCount * pdf + EPSILON)) / (4.0f * PI / (6.0f * resolution * resolution)));
            prefilterColor += textureLod(skyboxTex, L, mip).rgb * nDotL;
            totalWeight += nDotL;
        }
    }
    
    imageStore(outputTex, ivec3(gl_GlobalInvocationID), uvec4(packe5bgr9(vec4(prefilterColor / totalWeight, 1.0f))));
}